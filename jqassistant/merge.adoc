== Graph Merge

[[merge:Default]]
[role=group,includesConstraints="merge:*"]
== Default Rules

[[merge:MergeDuplicatedTypes]]
.Merges duplicated Java type nodes and their relationships
[source,cypher,role=concept]
----
MATCH
   (node:Java:Type)
WITH
   node.fqn AS name,
   COLLECT(node) AS nodes,
   COUNT(*) AS count
WHERE
   count > 1
CALL
   apoc.refactor.mergeNodes(nodes, {mergeRels: true}) YIELD node
RETURN
   node
----

[[merge:MergeDuplicatedMethods]]
.Merges duplicated methods and their relationships based on full path
[source,cypher,role=concept,requiresConcepts="merge:MergeDuplicatedTypes"]
----
MATCH
   (class)-[:DECLARES]->(method:Java:Method)
WITH
   method.signature AS name,
   class.fqn AS root,
   COLLECT(method) AS nodes,
   COUNT(*) AS count
WHERE
   count > 1
CALL
   apoc.refactor.mergeNodes(nodes, {mergeRels: true}) YIELD node
RETURN
   node
----

[[merge:MergeDuplicatedMavenArtifacts]]
.Merges duplicated Maven artifacts and their relationships based on name and group
[source,cypher,role=concept]
----
MATCH
   (node:Maven:Artifact)
WITH
   node.name AS name,
   node.group AS group,
   COLLECT(node) AS nodes,
   COUNT(*) AS count
WHERE
   count > 1
CALL
   apoc.refactor.mergeNodes(nodes, {mergeRels: true}) YIELD node
RETURN
   node
----
